WHITESPACE = _{ " " | "\t" | NEWLINE }

number = @{ ASCII_DIGIT+ }
ident = { ASCII_ALPHA+ }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\" | NEWLINE) ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
call = { ident ~ "(" ~ (comparison ~ ("," ~ comparison)*)? ~ ")" }

PLUS = @{ "+" }
MINUS = @{ "-" }
STAR = @{ "*" }
SLASH = @{ "/" }
PERCENT = @{ "%" }
BANG = @{ "!" }

expr = { term ~ ((PLUS | MINUS) ~ term)* }
term = { factor ~ ((STAR | SLASH | PERCENT) ~ factor)* }
factor = { ((MINUS | BANG) ~ primary) | primary }
primary = { "(" ~ comparison ~ ")" | call | number | ident | string }

GT = @{ ">" }
LT = @{ "<" }
EQ = @{ "==" }
NE = @{ "!=" }
GTE = @{ ">=" }
LTE = @{ "<=" }

comparison_op = { GT | LT | EQ | NE | GTE | LTE }
comparison = { expr ~ (comparison_op ~ expr)* }

let_stmt = { "let" ~ ident ~ "=" ~ comparison }
assign_stmt = { ident ~ "=" ~ comparison }
return_stmt = { "return" ~ comparison? }

fn_def = { "fn" ~ ident ~ "(" ~ (ident ~ ("," ~ ident)*)? ~ ")" ~ "{" ~ stmt* ~ "}" }

stmt = { (let_stmt | assign_stmt | return_stmt | comparison) ~ ";" }
prog = _{ (fn_def | stmt)* }
