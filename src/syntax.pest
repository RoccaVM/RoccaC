WHITESPACE = _{ " " | "\t" | NEWLINE }

number = @{ ASCII_DIGIT+ }
ident = { ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\" | NEWLINE) ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
call = { ident ~ "(" ~ (logic ~ ("," ~ logic)*)? ~ ")" }

struct_literal = { ident ~ "{" ~ (ident ~ ":" ~ logic ~ ("," ~ ident ~ ":" ~ logic)*)? ~ "}" }

ref_expr = { "&" ~ mut_specifier? ~ atom_expr }
deref = { "*" ~ ident }

PLUS = @{ "+" }
MINUS = @{ "-" }
STAR = @{ "*" }
SLASH = @{ "/" }
PERCENT = @{ "%" }
BANG = @{ "!" }

expr = { term ~ ((PLUS | MINUS) ~ term)* }
term = { factor ~ ((STAR | SLASH | PERCENT) ~ factor)* }
factor = { ((MINUS | BANG) ~ atom_expr) | ref_expr | atom_expr }
base_expr = { "(" ~ logic ~ ")" | struct_literal | call | number | deref | ident | string }

field_suffix = { "." ~ (ident | call) }
atom_expr = { base_expr ~ field_suffix* }

GT = @{ ">" }
LT = @{ "<" }
EQ = @{ "==" }
NE = @{ "!=" }
GTE = @{ ">=" }
LTE = @{ "<=" }

comparison_op = { GTE | LTE | EQ | NE | GT | LT }
comparison = { expr ~ (comparison_op ~ expr)* }

AND = @{ "&&" }
OR = @{ "||" }
logical_op = { AND | OR }
logic = { comparison ~ (logical_op ~ comparison)* }

mut_specifier = { "mut" }
ref_specifier = { "ref" | "&" }

data_type = { "&" ~ "mut"? ~ base_type | base_type }
base_type = { ident ~ ("<" ~ data_type ~ ">")? }

let_stmt = { "let" ~ mut_specifier? ~ ident ~ (":" ~ data_type)? ~ "=" ~ logic }
assign_stmt = { atom_expr ~ "=" ~ logic }
return_stmt = { "return" ~ logic? }

if_stmt = { "if" ~ logic ~ "{" ~ stmt* ~ "}" ~ else_if_stmt* ~ else_stmt? }
else_if_stmt = { "else if" ~ logic ~ "{" ~ stmt* ~ "}" }
else_stmt = { "else {" ~ stmt* ~ "}" }

while_loop = { "while" ~ logic ~ "{" ~ stmt* ~ "}" }

fn_def = { "fn" ~ ident ~ "(" ~ (ident ~ ":" ~ data_type ~ ("," ~ ident ~ ":" ~ data_type)*)? ~ ")" ~ ("->" ~ data_type)? ~ "{" ~ stmt* ~ "}" }

generic_spec = { "<" ~ ident ~ ("," ~ ident)* ~ ">" }
struct_def = { "struct" ~ ident ~ generic_spec? ~ "{" ~ (ident ~ ":" ~ data_type ~ ("," ~ ident ~ ":" ~ data_type)* ~ ","?)? ~ "}" }

compound_stmt = _{ struct_def | fn_def | if_stmt | while_loop }
simple_stmt = _{ let_stmt | assign_stmt | return_stmt | logic }

stmt = { (simple_stmt ~ ";") | compound_stmt }
prog = _{ stmt* }
