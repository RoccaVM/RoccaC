WHITESPACE = _{ " " | "\t" | NEWLINE }

number = @{ ASCII_DIGIT+ }
ident = { ASCII_ALPHA+ }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\" | NEWLINE) ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
call = { ident ~ "(" ~ (logic ~ ("," ~ logic)*)? ~ ")" }

PLUS = @{ "+" }
MINUS = @{ "-" }
STAR = @{ "*" }
SLASH = @{ "/" }
PERCENT = @{ "%" }
BANG = @{ "!" }

expr = { term ~ ((PLUS | MINUS) ~ term)* }
term = { factor ~ ((STAR | SLASH | PERCENT) ~ factor)* }
factor = { ((MINUS | BANG) ~ primary) | primary }
primary = { "(" ~ logic ~ ")" | call | number | ident | string }

GT = @{ ">" }
LT = @{ "<" }
EQ = @{ "==" }
NE = @{ "!=" }
GTE = @{ ">=" }
LTE = @{ "<=" }

comparison_op = { GT | LT | EQ | NE | GTE | LTE }
comparison = { expr ~ (comparison_op ~ expr)* }

AND = @{ "&&" }
OR = @{ "||" }
logical_op = { AND | OR }
logic = { comparison ~ (logical_op ~ comparison)* }

let_stmt = { "let" ~ ident ~ "=" ~ logic }
assign_stmt = { ident ~ "=" ~ logic }
return_stmt = { "return" ~ logic? }

if_stmt = { "if" ~ logic ~ "{" ~ stmt* ~ "}" ~ else_if_stmt* ~ else_stmt? }
else_if_stmt = { "else if" ~ logic ~ "{" ~ stmt* ~ "}" }
else_stmt = { "else {" ~ stmt* ~ "}" }

fn_def = { "fn" ~ ident ~ "(" ~ (ident ~ ("," ~ ident)*)? ~ ")" ~ "{" ~ stmt* ~ "}" }

compound_stmt = _{ fn_def | if_stmt }
simple_stmt = _{ let_stmt | assign_stmt | return_stmt | logic }

stmt = { (simple_stmt ~ ";") | compound_stmt }
prog = _{ stmt* }
